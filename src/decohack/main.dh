/*****************************************************************************
 * DECOHack Patch File
 * Main
 * Matthew DeCalzadilla
 ****************************************************************************/

#include <mbf21>
#include <friendly>

misc
{
	initialBullets 10
	initialHealth 50
}

// Bullets
ammo 0
{
	max 30
}

// Shells
ammo 1
{
	max 18
}

// Cells
ammo 2
{
	max 120
}

// Rockets
ammo 3
{
	max 15
}

// Make all hanging bodies non-solid
each thing from 129 to 134
{
	-SOLID
}

// Make all melee weapons silent
each weapon in (0, 4, 7)
{
	+SILENT
}

// Remove auto-switching on ammo pickup
each weapon from 0 to 1
{
	-AUTOSWITCHFROM
}

// Rename Rocket pickup for map editor convenience
thing 66 "Grenade" {}

// Rename Box of rockets pickup for map editor convenience
thing 67 "Box of grenades" {}

// Rename Chainsaw pickup for map editor convenience
thing 75 "Axe" {}

// Rename Rocket launcher pickup for map editor convenience
thing 76 "Hand grenades" {}

strings
{
	GOTARMOR "This armor should keep me safe."
	GOTMEGA "Now this is some heavy-duty armor!"
	GOTHTHBONUS "This strange potion numbs the pain a little..."
	GOTARMBONUS "I can repair my armor with this scrap."
	GOTSTIM "A first aid kit!"
	GOTMEDINEED "Thought I was a goner for sure..."
	GOTMEDIKIT "A surgical kit!"
	GOTSUPER "Woah! I feel younger!"
	GOTBLUECARD "A blue keycard."
	GOTYELWCARD "A yellow keycard."
	GOTREDCARD "A red keycard."
	GOTBLUESKUL "A glowing blue skull."
	GOTYELWSKUL "A glowing yellow skull."
	GOTREDSKULL "A glowing red skull."
	GOTINVUL "I feel invulnerable!"
	GOTBERSERK "I feel like I can punch through walls!!"
	GOTINVIS "I'm invisible!"
	GOTSUIT "This hazmat suit will protect me."
	GOTMAP "A map of the neighborhood?"
	GOTVISOR "Nightvision goggles. Let's hope the battery's not low..."
	GOTMSPHERE "I've never felt better!"
	GOTCLIP "More bullets for my pistol."
	GOTCLIPBOX "Wow. That's a lot of bullets."
	GOTROCKET "A grenade."
	GOTROCKBOX "A box of hand grenades!"
	GOTCELL "Some fuel."
	GOTCELLBOX "This is a lot of fuel!"
	GOTSHELLS "Shotgun shells."
	GOTSHELLBOX "A box of shotgun shells!"
	GOTBACKPACK "I can carry some more ammo in this backpack."
	GOTBFG9000 "What on Earth is this!?"
	GOTCHAINGUN "An SMG! Better watch my ammo."
	GOTCHAINSAW "An axe!"
	GOTLAUNCHER "Hand grenades!"
	GOTPLASMA "A flamethrower!?"
	GOTSHOTGUN "A shotgun!"
	GOTSHOTGUN2 "Nice! A double-barreled shotgun!"

	PD_BLUEO "I need a blue key to make this work..."
	PD_REDO "I need a red key to make this work..."
	PD_YELLOWO "I need a yellow key to make this work..."
	PD_BLUEK "I need a blue key for this..."
	PD_REDK "I need a red key for this..."
	PD_YELLOWK "I need a yellow key for this..."
	PD_BLUEC "I need a blue keycard for this..."
	PD_REDC "I need a red keycard for this..."
	PD_YELLOWC "I need a yellow keycard for this..."
	PD_BLUES "I need a blue skull for this..."
	PD_REDS "I need a red skull for this..."
	PD_YELLOWS "I need a yellow skull for this..."
	PD_ANY "I suppose any key could work..."
	PD_ALL3 "This needs three keys to open..."
	PD_ALL6 "Looks like I need six keys for this..."

	STSTR_CHOPPERS "And my axe!"
}

// Fist
weapon 0
{
	ammotype 5

	States
	{
		Fire:
			PUNG C 1
			PUNG D 5 A_Punch
			PUNG CBA 5
			Goto Ready
	}
}

// Pistol
weapon 1
{
	ammotype 0

	States
	{
		Fire:
			PISG A 4
			PISG B 1 Bright {
				A_ConsumeAmmo
				A_Light2
				A_WeaponSound("FRE002")
				A_WeaponBulletAttack(3.0, 3.0, 1, 12, 2)
			}
			PISG F 5 A_Light0
			PISG D 9
			PISG A 4 A_ReFire
			Goto Ready
	}
}

// Pump-action Shotgun
weapon 2
{
	ammotype 1

	States
	{
		Fire:
			SHTG B 1 Bright {
				A_ConsumeAmmo
				A_Light2
				A_WeaponSound("SHOTGN")
				A_WeaponBulletAttack(3.0, 3.0, 7, 8, 3)
			}
			SHTG D 2 A_Light0
			SHTG CAC 7
			SHTG D 7
			SHTG E 8 A_WeaponSound("FRE000")
			SHTG D 9
			SHTG C 8
			SHTG A 4
			SHTG A 4 A_ReFire
			Goto Ready
	}
}

// SMG
weapon 3 "SMG"
{
	ammotype 0
	ammopershot 2

	States
	{
		Fire:
			CHGG BCD 2 Bright {
				A_ConsumeAmmo
				A_Light2
				A_WeaponSound("FRE005")
				A_WeaponBulletAttack(6.0, 4.0, 2, 8, 3)
			}
			CHGG E 0 A_Light0
			CHGG E 4 A_ReFire
		Goto Ready
	}
}

// Spawns a Magic grenade spawner which spawns a real grenade.
// Gets around the DECOHack limitation specified above Magic grenade spawner.
weapon 4 "Hand grenade"
{
	ammotype 3

	States
	{
		Fire:
			MISG BC 2
			MISG D 6
			MISG E 12 A_WeaponSound("FRE001")
			MISG E 1 A_RefireTo(FireHold,0)
			Goto Throw
		FireHold:
			MISG E 1
			MISG E 1 A_RefireTo(FireHold,0)
			Goto Throw
		Throw:
			MISG F 2 {
				A_ConsumeAmmo
				A_WeaponProjectile(34, 0.0, 0.0, 10.0, 5.0)
			}
			MISG G 16 A_WeaponSound("SKESWG")
			Goto Ready
	}
}

weapon 7 "Axe"
{
	ammotype 5

	-NOTHRUST

	States
	{
		Ready:
			SAWG A 1 A_WeaponReady
			Loop
		Deselect:
			SAWG A 1 A_Lower
			Loop
		Select:
			SAWG A 1 A_Raise
			Loop
		Fire:
			SAWG C 8
			SAWG B 12
			SAWG C 1 A_WeaponSound("SKESWG")
			SAWG DDDDD 0 A_WeaponMeleeAttack(10,2,10.0, "SAWHIT",92.0)
			SAWG D 3 A_WeaponMeleeAttack(10,2,10.0, "SAWHIT",92.0)
			SAWG E 8
			SAWG D 8
			Goto Ready
	}
}

weapon 8 "Bearkiller"
{
	ammotype 1

	States
	{
		Fire:
			SHT2 A 2 {
				A_ConsumeAmmo
				A_GunFlash
				A_WeaponSound("DSHTGN")
				A_WeaponBulletAttack(2.0, 1.0, 21, 7, 5)
			}
			SHT2 C 40 {
				A_Light0
				A_CheckReload
			}
			SHT2 D 6
			SHT2 F 30 A_OpenShotgun2
			SHT2 G 6
			SHT2 H 15 A_WeaponSound("DBLOAD")
			SHT2 I 8
			SHT2 J 4 A_WeaponSound("DBCLS")
			SHT2 K 6
			SHT2 A 3
			Goto Ready
		Flash:
			SHT2 B 2 Bright A_Light2
			Goto LightDone
	}
}

thing 2 "Former Human"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			POSS H 5
			POSS I 5 A_Scream
			POSS J 5 A_Fall
			POSS K 5
			POSS L 150
			POSS K 5 A_XScream
			POSS JIH 5
			POSS H 0 A_SpawnObject(2)
			Stop
		Raise:
			SPOS TSRQPONM 5
			Goto See
	}
}

thing 3 "Former Sergeant"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_Fall
			SPOS K 5
			SPOS L 175
			SPOS K 5 A_XScream
			SPOS JIH 5
			SPOS H 0 A_SpawnObject(3)
			Stop
		Raise:
			SPOS TSRQPONM 5
			Goto See
	}
}

// Revenant
thing 6
{
	States
	{
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_Fall
			SKEL P 7
			SKEL Q 875
			SKEL Q 5 A_XScream
			SKEL PONML 5
			SKEL L 0 A_SpawnObject(6)
			Stop
	}
}

thing 11 "Chaingunner"
{
	painchance 256

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 0

	States
	{
		Death:
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_Fall
			CPOS KLM 5
			CPOS N 250
			CPOS N 5 A_XScream
			CPOS MLKJIH 5
			CPOS H 0 A_SpawnObject(11)
			Stop
	}
}

// Imp
thing 12
{
	painchance 256

	States
	{
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_Fall
			TROO M 150
			TROO M 8 A_XScream
			TROO L 8
			TROO KJI 6
			TROO I 0 A_SpawnObject(12)
			Stop
	}
}

// Demon
thing 13
{
	States
	{
		Death:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_Fall
			SARG M 4
			SARG N 300
			SARG N 5 A_XScream
			SARG MLKJI 5
			SARG I 0 A_SpawnObject(13)
			Stop
	}
}

// Modify rocket to get around a DECOHack limitation on A_WeaponProjectile
// A_WeaponProjectile can only spawn things flagged with MISSILE
// which prevents the grenade from bouncing around before exploding.
thing 34 "Magic grenade spawner"
{
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_SpawnObject(151,-90.0,-10.0,0.0,0.0,-0.5,25.0,5.0)
			Stop
	}
}

// Hell knight
thing 69
{
	States
	{
		Death:
			BOS2 I 8
			BOS2 J 8 A_Scream
			BOS2 K 8
			BOS2 L 8 A_Fall
			BOS2 MN 8
			BOS2 O 525
			BOS2 N 8 A_XScream
			BOS2 MLKJI 8
			BOS2 I 0 A_SpawnObject(69)
			Stop
	}
}

// An actual grenade which will bounce around the map before detonating.
thing 151 : thing 34 "Grenade in flight"
{
	Health 5
	Mass 256

	+SHOOTABLE
	-NOBLOCKMAP
	-NOGRAVITY
	-MISSILE //Â—does not replicate behavior from hovercab.wad
	+NOBLOOD
	+BOUNCES
	// +TOUCHY

	States
	{
		Spawn:
			SP00 ABCDEF 2
			SP00 ABCDEF 3
			SP00 ABCDEF 5
			SP00 ABC 8
			SP00 C 5
			Goto Death
		Death:
			MISL B 8 Bright {
				A_PlaySound("BAREXP")
				A_NoiseAlert
				A_RadiusDamage(192,256)
			}
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}

thing 152 "Helicopter"
{
	//$category Decoration
	Ednum 6002

	Radius 56
	Height 112
	Mass 1000

	+NOBLOCKMAP

	States
	{
		Spawn:
			SP02 A -1
			Stop
	}
}

thing 153 "Something that should not be"
{
	//$category Monsters
	Ednum 6001

	Health 1500
	Speed 48
	Radius 20
	Height 72
	Reactiontime 8
	Painchance 170
	Mass 1000
	Damage 4096

	// Flags
	+SOLID
	+SHOOTABLE
	+COUNTKILL
	+LOGRAV

	seesound "FRE003"

	States
	{
		Spawn:
			SP01 E 5 A_Look
			SP01 E 5 A_Turn(18)
			Loop
		See:
			SP01 E 20 {
				A_Chase
				A_JumpIfTargetInSight(CheckRange)
			}
			Loop
		CheckRange:
			SP01 A 1 A_JumpIfTargetCloser(Attack, 2048.0)
			Goto See
		Attack:
			SP01 F 10 {
				A_FaceTarget
				A_RandomJump(AttackSound1, 96)
				A_RandomJump(AttackSound2, 96)
			}
			Goto JumpAtPlayer
		AttackSound1:
			SP01 F 0 A_PlaySound(FRE004)
			Goto JumpAtPlayer
		AttackSound2:
			SP01 F 0 A_PlaySound(FRE003)
			Goto JumpAtPlayer
		JumpAtPlayer:
			SP01 G 0 A_RandomJump(Spawn, 32)
			SP01 GH 10 A_FaceTarget
			SP01 H 10 A_SkullAttack
			SP01 HHH 10 A_FaceTarget
			SP01 H 10 {
				A_FaceTarget
				A_RandomJump(Pain, 32)
			}
			Goto See
		Pain:
			SP01 EEEEEEEEEE 10 A_FaceTarget
			Goto See
		Death:
			SP01 J 10 A_XScream
			SP01 K 7 A_Fall
			SP01 LMNO 5
			SP01 P 700
			SP01 O 5 A_XScream
			SP01 NML 5
			SP01 K 7
			SP01 J 10
			SP01 J 0 A_SpawnObject(153)
			Stop
	}
}